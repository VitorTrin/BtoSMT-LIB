Queremos a partir de uma máquina em B e um teste criar um  código em SMT-LIB que
me forneça valores válidos para executar o teste.

1- Lógicas utilizadas

    Na primeira linha deve ser especificando quais são as lógicas que serão utilizadas.
Em geral lógicas menores e mais simples tendem a serem mais rápidas.Como em geral
não estamos preocupados com velocidade, já que nossos testes são simples, recomendo
utilizar (set-logic ALL) onde for possível. ALL é do padrão SMT-LIB 2.5, logo apenas
solvers mais atualizados o suportam. CVC4 suporta, e Z3 o trata como lógica desconhecida,
na qual ele funciona surpreendentemente bem.

2- Gerar modelo

    A segunda linha especificamos as opções de comando. Para nosso objetivo precisamos
de (set-option :produce-models true) para forçar o solver a gerar modelos e permite
o comando (get-model), que retorna todo o modelo, ou (get-value), para o valor de
varáveis em especial.

3- Invariante

    Os valores para teste só são válidos se atenderem aos invariantes, então começamos
nosso trabalho pelo invariante e declaração de variáveis da máquina B. Cada linha
deve ser traduzida em declarações de tipos (declare-sort), declaração de funções
(declare-fun) ou asserções (assert). Em SMT-LIB, não há diferença entre funções
e variáveis.

    Exemplo:

    Máquina B:
    SETS
      PERSON ;

    O que pode ser entendido como:  Existe o conjunto PERSON.

    Como foi traduzido:

    (declare-sort PERSON 0)

    Existe o tipo PERSON com aridade 0. Para falar do conjunto com todos os
elementos de tipo PERSON usamos "as univset (Set PERSON)".

	Para descrever funções já definidas, como por exemplo

	Máquina B:
    male <: PERSON &
    female <: PERSON &
    male /\ female = {}

	nos utilizamos de asserções

	SMT-LIB:
    (declare-fun male () (Set PERSON))
    (declare-fun female () (Set PERSON))
    (assert (= (intersection male female) (as emptyset (Set PERSON))))

	Essa asserção diz que a interseção entre male e female é igual a um conjunto
vazio de elementos PERSON.

3.1- Conjuntos

    Na fase de tradução de variáveis é preciso decidir se será possível simplificar
conjuntos por variáveis mais simples. Lógica de conjuntos é algo extremamente
importante por ser o mais próximo de B que SMT-LIB chega e por ser altamente condensada,
mas por outro lado conjuntos não são definidos no SMT-LIB standard, então o único
solver que aceita sintaxe de conjuntos como entrada de SMT-LIB é o CVC4. O SMT-LIB
standard usa lógica de conjuntos para explicar como definir uma nova lógica, e
esse exemplo de como seria a lógica é usado por CVC4.

4- Inicialização

    A inicialização é ignorada, já que animar a máquina e determinar se o estado que
retornamos é alcançável não nos interessa.

5- Operação

    Como os testes visam testar as operações, é necessário definir as precondições
das operações.Os efeitos e lógica interna da operação não importam, uma vez que
o objetivo é obter os valores válidos para a execução do teste.Um exemplo de
precondição de uma operação:

  OPERATIONS
  die ( nn ) =
    PRE
      nn : PERSON &
      nn : male \/ female

    Existem diversas opções de tradução para operações.

5.1- A tradução mais direta

    (declare-fun die (PERSON) Bool)
    (assert
        (forall ((pp PERSON))
            (=
                (die pp)
                (member p (union male female))
            )
        )
    )

    Ou seja, para todos os elementos pp de person, die pp retorna um booleano, e
o valor desse booleano é o mesmo que a fórmula "pp é membro da união de male com
female".

5.2- A abreviação

    Esse modelo de declare-fun seguido de um assert com quantificador e = possui
uma abreviação, o define-fun

    (define-fun die ((pp PERSON)) Bool (member pp (union male female)))

    Um dos objetivos desse abreviação é dar ao solver oportunidade de reconhecer
facilmente definições de funções e possivelmente processá-los especialmente
(The SMT-LIB Standard 2.0 pag 91). Em certas ocasiões um declare-fun seguido de
assert gera uma resposta unknown, enquanto um define-fun gera sat.

5.3- Substituição

    A sintaxe do define-fun pode não ser intuitiva e o declare-fun seguido de
assert pode gerar incerteza. Como alternativa, é possível simplesmente substituir
cada referência a operação pela sua expressão equivalente.

6- Os testes

    Definidos os invariantes e as operações a máquina B está toda definida. Agora
falta definir os testes. Como os testes são independentes entre eles, um jeito
prático de evitar que os testes interfiram uns nos outros é cercar as linhas de teste
de (push 1) e (pop 1). O comando (push n) empilha n níveis vazios de asserção na
pilha de asserção.
    Exemplo de teste:

    Formula: account_balance >= mm & mm : INT & mm > 0

    SMT-LIB:

    (push 1)

    (declare-fun mm () Int)
    (assert (>= account_balance mm))
    (assert (> mm 0))

    (check-sat)
    (get-value (account_id account_balance x1))

    (pop 1)

    account_id e account_balance já foram definidos na máquina.

    O comando (check-sat) checa se a conjunção de todas as fórmulas no contexto
atual é satisfatível, retornando sat caso seja satisfatível, unsat caso não seja,
e unknown se o resultado foi inconclusivo(The SMT-LIB Standard 2.0 pag 64).Neste
caso, sat significa que existe um conjunto de valores que podem ser utilizados
para a execução do teste, unsat significa que o teste não possui entradas válidas,
e unknown deve ser avaliado caso a caso.
    O comando (get-value (termo)) retorna o valor que esse termo possui no modelo
atual, e são esses valores que são o objetivo final e serão utilizados para a
execução do teste. get-value só pode ser executado depois de um (check-sat) e esse
(check-sat) não pode ter retornado unsat.
    Para facilitar a identificação de qual retorno é de que teste em uma série
de testes o comando (echo "string") pode ser utilizado.

6.1- Testes com substituição

    No caso em que a tradução de operações utilizada é a por substituição, os
usos nos testes devem ser subistituídos um a um

    Exemplo em tradução direta:

    (declare-fun substitute (PLAYER PLAYER) Bool)
    (assert
        (forall ((pp PLAYER) (rr PLAYER))
            (=
                (substitute pp rr)
                (and
                    (member pp Team)
                    (not(member rr Team))
                )
            )
        )
    )

    (push 1)

    (declare-fun player1 () PLAYER)
    (declare-fun player2 () PLAYER)

    (assert (not (member player1 Team)))
    (assert (not (not (member player2 Team))))
    (assert (substitute player1 player2))

    (check-sat)
    (get-value (Team))

    (pop 1)

    se torna, em substituição

    (push 1)

    (declare-fun player1 () PLAYER)
    (declare-fun player2 () PLAYER)

    (assert (not (member player1 Team)))
    (assert (not (not (member player2 Team))))
    (assert (and (member player1 Team) (not (member player2 Team))))

    (check-sat)
    (get-value (Team))

    (pop 1)
